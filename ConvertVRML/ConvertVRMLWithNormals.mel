/* ConvertVRML.mel - Steven Dao - 8 February 2014.
 * This script converts a polygon mesh in Maya to the VRML-style representation used in
 * C S 354 project 1.
 * Note, for a complex object, the bottleneck of printing to the Maya console means that
 * this script may take a few seconds to run.
 * This script will use the world-space (not object-space) coordinates of your object.
 * Thus, you will have to scale them to the appropriate size first.
 * This version also computes per-face, per-vertex normals.
 * The normals are grouped by face (in the order of the indices[] array),
 * and then by each vertex of the face (in the order that the vertex appeared in the
 * indices[] array).
 */

PolySelectConvert 3; // Change selection to vertices.
string $verts[] = `ls -selection -flatten`;
float $allVerts[];
clear $allVerts; // Clear anything leftover from last time, otherwise we keep appending.
for ($i = 0; $i < `size $verts`; $i++) {
    // Get coord for each vertex.
    $val = $verts[$i];
    vector $coords = `xform -q -ws -t $val`;
    $allVerts[`size $allVerts`] = $coords.x;
    $allVerts[`size $allVerts`] = $coords.y;
    $allVerts[`size $allVerts`] = $coords.z;
}

PolySelectConvert 1; // Change selection to faces.
string $faces[] = `ls -selection -flatten`;
int $allFaceIndices[];
clear $allFaceIndices;
float $allFaceVtxNormals[];
clear $allFaceVtxNormals;
for ($i = 0; $i < `size $faces`; $i++) {
    $val = $faces[$i];
    
    // Get vertex info.
    string $faceInfo[] = `polyInfo -faceToVertex $val`;
    string $tokens[];
    tokenize $faceInfo[0] " :\n\r" $tokens;
    int $t;
    for ($t = 2; $t < `size $tokens`; $t++) {
        $allFaceIndices[`size $allFaceIndices`] = $tokens[$t];
    }
    $allFaceIndices[`size $allFaceIndices`] = -1;
    
    // Get per-vertex normals info.
    string $faceVtexes[] = `polyListComponentConversion -fromFace -toVertexFace $val`;
    string $flatFaceVtexes[] = `ls -flatten $faceVtexes`;
    
    for ($faceVtx in $flatFaceVtexes) {
        vector $faceNormal = `polyNormalPerVertex -q -xyz $faceVtx`;
        $allFaceVtxNormals[`size $allFaceVtxNormals`] = $faceNormal.x;
        $allFaceVtxNormals[`size $allFaceVtxNormals`] = $faceNormal.y;
        $allFaceVtxNormals[`size $allFaceVtxNormals`] = $faceNormal.z;
    }
}

print "/* Automatically generated VRML data. */\n";

print "GLfloat vertices[] = {";
for ($i = 0; $i < `size $allVerts`; $i++) {
    if ($i % 3 == 0) {
        print "\n\t";
    }
    print $allVerts[$i];
    print ", ";
}
print "\n};\n";

print "GLint indices[] = {";
int $prevIndex = -1;
for ($i = 0; $i < `size $allFaceIndices`; $i++) {
    if ($prevIndex == -1) {
        print "\n\t";
    }
    $prevIndex = $allFaceIndices[$i];
    print $prevIndex;
    print ", ";
}
print "\n};\n";

print "GLfloat normals[] = {";
for ($i = 0; $i < `size $allFaceVtxNormals`; $i++) {
    if ($i % 3 == 0) {
        print "\n\t";
    }
    print $allFaceVtxNormals[$i];
    print ", ";
}
print "\n};\n";
